Multichar_Symbols
%<V%>
%<1SgSubj%>
%<1PlSubj%>
%<2SgSubj%>
%<2PlSubj%>
%<3SgSubj%>
%<3PlSubj%>
%<1SgObj%>
%<2SgObj%>
%<3SgObj%>
%<1PlObj%>
%<2PlObj%>
%<3PlObj%>
%<1SgRec%>
%<2SgRec%>
%<3SgRec%>
%<1PlRec%>
%<2PlRec%>
%<3PlRec%>
%<Hon%>
%<Refl%>
%<Pres%>
%<Pret%>
%<Fut%>
%<Opt%>
%<Plup%>   !Pluperfect
%<Cfact%>  !Counterfactual
%<PerfDirTow%> !Directional Toward
%<PerfDirAwa%> !Directional Away
%<ImpDirTow%> !Directional Toward
%<ImpDirAwa%> !Directional Away
%<OptDirTow%> !Directional Toward

! Flag Diacritics
@P.PST.ON@  !Past (indicates that the augment 'o-' was used)
@R.PST@     !Require Past
@D.PST@     !Prohibit Past
@P.PLS.ON@  !Plural Subject
@R.PLS@     !Require Plural Subject
@D.PLS@     !Prohibit Plural Subject
@P.INTR.ON@ !Intranstive Verb
@R.INTR@    !Require Intransitive Verb
@D.INTR@    !Prohibit Intransitive Verb
@P.OPT.ON@  !Optative (indicates that 'xi-' subject was used)
@R.OPT@     !Require Optative
@D.OPT@     !Prohibit Optative
@P.2P.ON@   !Second Person subj or obj (for proper usage of the honorific)
@R.2P@      !Require 2nd Person subj or obj
@P.MOTION.ON@
@R.MOTION@
@P.DITRANS.ON@
@D.DITRANS@
@P.OBJFILLED.ON@ !If the object prefix slot is filled, then the indefinite
                 ! object can only appear when the verb is bitransitive.
@R.OBJFILLED@
@D.OBJFILLED@

LEXICON Root
%<V%>:0 VerbalNuclearClause ;

LEXICON VerbalNuclearClause
@P.PST.ON@:@P.PST.ON@o^ SubjectPrefixes ;  ! past augment
0:0 SubjectPrefixes ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Subject Prefixes in Nahuatl are mostly single consonants (e.g. 'n-', 't-').
! In Classical Nahuatl, 'i'-epenthesis would occur after the prefix to satisfy
! syllable structure (e.g. *n#cochi, ni#cochi, but n#acic).
!
! * In the Omitl√°n variety of nhi, the 'i' is always inserted.
! * In Tenango nhi, 'i' is sometimes epenthesized before the consonant
!   depending on stress location (eg "it#niqui").
!
! The 'i'-epenthesis will be handled via rewrite rules.
!
! Additionally, nhi variants, unlike classical or other contemporary dialects,
! appear to have 'se-' as an alternative 1st person plural subject prefix.
! Unlike 'ti-', 'se-' does not co-occur with plural suffixes (i.e. the rest of
! the verb behaves as if it had a singular subject).
!
LEXICON SubjectPrefixes
%<1SgSubj%>:n^                                                ObjectPrefixes ;
!%<2SgSubj%>@P.2P.ON@:@P.2P.ON@t^                              ObjectPrefixes ;
!%<3SgSubj%>:0^                                                ObjectPrefixes ;
!%<1PlSubj%>@P.PLS.ON@:@P.PLS.ON@t^                            ObjectPrefixes ;
!%<1PlSubj%>:ze^                                               ObjectPrefixes ;
!%<2PlSubj%>@P.PLS.ON@:@P.PLS.ON@n^                            ObjectPrefixes ;
!%<3PlSubj%>@P.PLS.ON@:@P.PLS.ON@0^                            ObjectPrefixes ;
!%<Opt%>%<2SgSubj%>@P.OPT.ON@@P.2P.ON@:@P.OPT.ON@@P.2P.ON@x^   ObjectPrefixes ;
!%<Opt%>%<2PlSubj%>@P.PLS.ON@@P.OPT.ON@:@P.PLS.ON@@P.OPT.ON@x^ ObjectPrefixes ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! No object prefix for intransitive verbs. Otherwise, use appropriate nhi
! object prefix.
!
LEXICON ObjectPrefixes
@P.INTR.ON@:@P.INTR.ON@0            HonorificDirectional ;
0:0                                 HonorificDirectional ;
!%<1SgObj%>@P.OBJFILLED.ON@:@P.OBJFILLED.ON@nech^                    HonorificDirectional ;
!%<2SgObj%>@P.2P.ON@@P.OBJFILLED.ON@:@P.2P.ON@@P.OBJFILLED.ON@mitz^  HonorificDirectional ;
!%<3SgObj%>@P.OBJFILLED.ON@:@P.OBJFILLED.ON@K^                       HonorificDirectional ;
!%<1PlObj%>@P.OBJFILLED.ON@:@P.OBJFILLED.ON@tech^                    HonorificDirectional ;
!%<2PlObj%>@P.OBJFILLED.ON@:@P.OBJFILLED.ON@mech^                    HonorificDirectional ;
!%<3PlObj%>@P.OBJFILLED.ON@:@P.OBJFILLED.ON@Kim^                     HonorificDirectional ;

!%<1SgRec%>:nech^                    DiTransDirObj ;
!%<2SgRec%>@P.2P.ON@:@P.2P.ON@mitz^  DiTransDirObj ;
!%<3SgRec%>:K^                       DiTransDirObj ;
!%<1PlRec%>:tech^                    DiTransDirObj ;
!%<2PlRec%>:mech^                    DiTransDirObj ;
!%<3PlRec%>:Kim^                     DiTransDirObj ;


!LEXICON DiTransDirObj
!%<3PlDirObj%>@P.DITRANS.ON@:@P.DITRANS.ON@im^ HonorificDirectional ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! In nhi, 'on-', which in Classical nahuatl is the 'away from' directional, has
! come to be used exclusively to express deference. The 'toward' directional
! prefix, 'hual-', is still used to indicate directionality toward the speaker,
! but the 'away from' directionality is no longer achievable with 'on-' due to
! the precedence of 'on-'s usage as an Honorific marker.
!
! The 'on-' honorific morpheme also triggers regressive vowel harmony (i -> o).
!
LEXICON HonorificDirectional
!%<Hon%>@R.2P@:@R.2P@on^                   Reflexive ;
!%<DirTow%>@R.MOTION@:@R.MOTION@ual^       Reflexive ;
0:0                                       Reflexive ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Not every verb can be reflexive, so maybe roll the reflexive prefix into the
! verb stem lexical entry?
!
LEXICON Reflexive
!%<Refl%>:mo^ VerbBase ;
%<IndefObjThing%>@D.INTR@@D.OBJFILLED@:@D.INTR@@D.OBJFILLED@tla^   VerbBase ;
%<IndefObjPerson%>@D.INTR@@D.OBJFILLED@:@D.INTR@@D.OBJFILLED@te^   VerbBase ;
!0:0                      VerbBase ;


LEXICON VerbBase
@R.INTR@:@R.INTR@ Intransitive ;
@D.INTR@:@D.INTR@ Transitive ;

LEXICON Intransitive
@D.OPT@:@D.OPT@               IntransitiveBase1 ;
@R.PST@@D.OPT@:@R.PST@@D.OPT@ IntransitiveBase2 ;
@D.PST@:@D.PST@               IntransitiveBase3 ;

LEXICON Transitive
@D.OPT@:@D.OPT@               TransitiveBase1 ;
@R.PST@@D.OPT@:@R.PST@@D.OPT@ TransitiveBase2 ;
0:0                           TransitiveBase3 ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Verbal Base 1 is used for forming the simple present tense and the Imperfect.
! It is generally the "canonical" or "dictionary" form.
!
LEXICON IntransitiveBase1
cochi:cochi^                Base1Suffix ; !"dormir"
choca:choca^                Base1Suffix ; !"llorar"
panoa:panoa^                Base1Suffix ; !"pasar"
quiza:Kiza^                 Base1Suffix ; !"salir"

LEXICON TransitiveBase1
itta:itta^                  Base1Suffix ; !"ver"
caqui:KaKi^                 Base1Suffix ; !"escuchar"
notza:notza^                Base1Suffix ; !"llamar"
tlalia:tlalia^              Base1Suffix ; !"poner"
mati:mati^                  Base1Suffix ; !"saber"
piya:piya^                  Base1Suffix ; !"tener"
temoa:temoa^                Base1Suffix ; !"buscar"
ihtoa:ihtoa^                Base1Suffix ; !"decir"
palehuia:palehuia^          Base1Suffix ; !"apoyar"
ilhuia:ilhuia^              Base1Suffix ; !"contar/decir"


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Verbal base 2 is used to form the preterite.
!
LEXICON IntransitiveBase2
cochi:coch^                Base2Suffix ; !"dormir"
choca:choca^               Base2Suffix ; !"llorar"
panoa:pano^                Base2Suffix ; !"pasar"
quiza:Kiz^                 Base2Suffix ; !"salir"

LEXICON TransitiveBase2
itta:itta^                 Base2Suffix ; !"ver"
caqui:KaK^                 Base2Suffix ; !"escuchar"
notza:notz^                Base2Suffix ; !"llamar"
tlalia:tlalih^             Base2Suffix ; !"poner"
mati:mati^                 Base2Suffix ; !"saber"
piya:pix^                  Base2Suffix ; !"tener"
temoa:temoh^               Base2Suffix ; !"buscar"
ihtoa:ihto^                Base2Suffix ; !"decir"
palehuia:palehuih^         Base2Suffix ; !"apoyar"
ilhuia:ilhuih^             Base2Suffix ; !"contar/decir"


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Verbal Base 3 is used to form the future as well as the optative.
!
LEXICON IntransitiveBase3
cochi:cochi^              Base3Suffix ; !"dormir"
choca:choca^              Base3Suffix ; !"llorar"
panoa:pano^               Base3Suffix ; !"pasar"
quiza:Kiza^               Base3Suffix ; !"salir"

LEXICON TransitiveBase3
itta:itta^                Base3Suffix ; !"ver"
caqui:KaKi^               Base3Suffix ; !"escuchar"
notza:notza^              Base3Suffix ; !"llamar"
tlali:tlali^              Base3Suffix ; !"poner"
mati:mati^                Base3Suffix ; !"saber"
piya:piya^                Base3Suffix ; !"tener"
temoa:temo^               Base3Suffix ; !"buscar"
ihtoa:ihto^               Base3Suffix ; !"decir"
palehuia:palehui^         Base3Suffix ; !"apoyar"
ilhuia:ilhui^             Base3Suffix ; !"contar/decir"


LEXICON Base1Suffix
@D.PST@:@D.PST@ PresentTense ;
@R.PST@:@R.PST@ Imperfect ;

LEXICON Base2Suffix
0:0 Preterite ;
0:0 Pluperfect ;

LEXICON Base3Suffix
@D.OPT@@D.PST@:@D.OPT@@D.PST@ Future ;
@D.OPT@@D.PST@:@D.OPT@@D.PST@ Counterfactual ;
@R.OPT@@D.PST@:@R.OPT@@D.PST@ Optative ;

@D.OPT@:@D.OPT@               PerfectiveDirectionalToward ;
@D.OPT@:@D.OPT@               PerfectiveDirectionalAway ;
@D.OPT@@D.PST@:@D.OPT@@D.PST@ ImperfectiveDirectionalToward ;
@D.OPT@@D.PST@:@D.OPT@@D.PST@ ImperfectiveDirectionalAway ;
@R.OPT@@D.PST@:@R.OPT@@D.PST@ OptativeDirectonalToward ;


LEXICON PresentTense
%<Pres%>@D.PST@@D.PLS@:@D.PST@@D.PLS@0  # ;
%<Pres%>@R.PLS@:@R.PLS@PL # ;

LEXICON Imperfect
%<Imp%>@D.PLS@:@D.PLS@ya  # ;
%<Imp%>@R.PLS@:@R.PLS@yah # ;

LEXICON Preterite
%<Pret%>@D.PLS@:@D.PLS@K  # ;
%<Pret%>@R.PLS@:@R.PLS@Ke # ;

LEXICON Pluperfect
%<Plup%>@D.PLS@:@D.PLS@Ka   # ;
%<Plup%>@R.PLS@:@R.PLS@Kah  # ;

LEXICON Future
%<Fut%>@D.PLS@:@D.PLS@z    # ;
%<Fut%>@R.PLS@:@R.PLS@zque # ;

LEXICON Counterfactual
%<Cfact%>@D.PLS@:@D.PLS@zKia # ;
%<Cfact%>@R.PLS@:@R.PLS@zKiah # ;

LEXICON Optative
@D.PLS@:@D.PLS@0   # ;
@R.PLS@:@R.PLS@Kan # ;

Lexicon PerfectiveDirectionalToward
%<PerfDirTow%>@D.PLS@:@D.PLS@Ko # ;
%<PerfDirTow%>@R.PLS@:@R.PLS@Koh # ;

Lexicon ImperfectiveDirectionalToward
%<ImpDirTow%>@D.PLS@:@D.PLS@Kiuh # ;
%<ImpDirTow%>@R.PLS@:@R.PLS@Kiuih # ;

Lexicon OptativeDirectonalToward
%<OptDirTow%>@D.PLS@:@D.PLS@Ki # ;
%<OptDirTow%>@R.PLS@:@R.PLS@Kih # ;

Lexicon PerfectiveDirectionalAway
%<PerfDirAwa%>@D.PLS@:@D.PLS@to # ;
%<PerfDirAwa%>@R.PLS@:@R.PLS@toh # ;

Lexicon ImperfectiveDirectionalAway
%<ImpDirAwa%>@D.PLS@:@D.PLS@tiuh # ;
%<ImpDirAwa%>@R.PLS@:@R.PLS@ih # ;
