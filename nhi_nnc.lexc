Multichar_Symbols
%<Pos%>
%<N%>
%<Abs%>
%<Pl%>
%<Poss1Sg%>
%<Poss2Sg%>
%<Poss3Sg%>
%<Poss3Pl%>
%<Poss2Pl%>
%<Poss1Pl%>
%<Dim%>
%<Dim1%>
%<Dim2%>
@U.poss.ON@
@R.poss@
@D.poss@
@U.suff.tl@
@U.suff.tli@
@U.suff.li@
@U.suff.in@
@U.suff.other@
@C.suff@
@D.suff.li@

LEXICON Root
NominalNuclearClause ;

LEXICON NominalNuclearClause
NominalStem ;

LEXICON NominalStem
Stem ;
@U.poss.ON@:@U.poss.ON@ Poss ;


LEXICON Poss
%<Poss1Sg%>:no^ Stem ;
%<Poss2Sg%>:mo^ Stem ;
%<Poss3Sg%>:i^ Stem ;
%<Poss1Pl%>:to^ Stem ;

!
!In San Miguel Tenango, the 2nd person plural possessive prefix is "namo-",
! whereas in Omitlan, it appears to be "nimo-".
!
%<Poss2Pl%>:nimo^ Stem ;
%<tenango%>%<Poss2Pl%>:namo^ Stem ;
%<tenango%>%<Poss2Pl%>:namo^ Stem ;
%<Poss3Pl%>:im^ Stem ;


LEXICON Stem
@U.suff.tl@:@U.suff.tl@ TlStem ;
@U.suff.tli@:@U.suff.tli@ TliStem ;
@U.suff.li@:@U.suff.li@ LiStem ;
@U.suff.in@:@U.suff.in@ InStem ;
@U.suff.other@:@U.suff.other@ SuffixlessStem ;


LEXICON TlStem
amatl:ama^ Suff ;                       !"papel/libro"
ciuatl:cihua^ Suff ;                    !"woman"
oquichtl:oquich^ Suff ;                 !"male"
tlacatl:tlaca^ Suff ;                   !"man"
tototl:toto^ Suff ;                     !"pajaro"
ihuitl:ihui^ Suff ;                     !"gallina"
tipetl:tipe^ Suff ;                     !"cerro"
cacalotl:cacalo^ Suff ;                 !"cuervo"
tapalcatihuilotl:tapalcatihuilo^ Suff ; !"contenedor de agua"
tiquitl:tiqui^ Suff ;                   !"trabajo"
tlaltitl:tlalti^ Suff ;                 !"earth rock"
atl:a^ Suff ;                           !"agua"
nacatl:naca^ Suff ;                     !"carne"
ichcatl:ichca^ Suff ;                   !"borrego"
tipostl:tipos^ Suff ;                   !"carro/algo metalico"
yolotl:yolo^ Suff ;                     !"corazon"
conetl:cone^ Suff ;                     !"bebe"
cosamalotl:cosamalo^ Suff ;             !"arcoiris"
ichpocaconetl:ichpocacone^ Suff ;       !"girl baby"
quichconetl:quichcone^ Suff ;           !"male baby"
coyotl:coyo^ Suff ;                     !"coyote"

! todo: Figure out how to deal with these slightly different forms.
xihuitl:xiW^ Suff ;                    !"hierba/year" (noxiuh)
!caxitl: cax^ Suff ;                    !"plato"       (nocax, caxme)

LEXICON TliStem
tochtli:toch^ Suff;        !"conejo"
itzcuintli:itzcuin^ Suff;  !"perro"
pahtli:pah^ Suff;          !"medicina"
poctli:poc^ Suff;          !"humo"
metztli:metz^ Suff;        !"luna"
mixtli:mix^ Suff           !"nube


LEXICON LiStem
calli:cal^ Suff ;
citlalli:citlal^ Suff ;


!
! These noun stems are usually animals.
!
LEXICON INStem
chapolin:chapol ;   !"chapulin"
tamasolin:tamasol ; !"rana"
ayotachin:ayotoch ; !"armadillo"


LEXICON SuffixlessStem
tempil:tempil^ Suff ;           !"pico"
mahtlapal:mahtlapal^ Suff ;     !"ala"
tonal:tonal^ Suff ;             !"day/sun"
caxtil:caxtil^ Suff ;           !"gallina"
patox:patox^ Suff ;             !"pato"
ixtololo:ixtololo^ Suff ;       !"ojo"
tlapitlanil:tlapitlanil^ Suff ; !"rayo"


LEXICON Suff
0:0 UnpossessedEndings ;
0:0 PossessedEndings ;
!@C.suff@%#:@C.suff@0 Stem ; ! Compound Nouns

LEXICON UnpossessedEndings
! Singulars
@U.suff.other@@D.poss@%<N%>%<Abs%>:@U.suff.other@@D.poss@ # ;
@U.suff.li@@D.poss@%<N%>%<Abs%>:@U.suff.li@@D.poss@li # ;
@U.suff.tl@@D.poss@%<N%>%<Abs%>:@U.suff.tl@@D.poss@tl # ;
@U.suff.tli@@D.poss@%<N%>%<Abs%>:@U.suff.tli@@D.poss@tli # ;
@U.suff.in@@D.poss@%<N%>%<Abs%>:@U.suff.in@@D.poss@in # ;

! Plurals
@U.suff.li@@D.poss@%<N%>%<Pl%>:@U.suff.li@@D.poss@lime # ;
@D.suff.li@@D.poss@%<N%>%<Pl%>:@D.suff.li@@D.poss@me # ;

! Dim.
@D.poss@%<N%>:@D.poss@ Diminutive ;


LEXICON PossessedEndings
@R.poss@%<N%>:@R.poss@0 # ;
@R.poss@%<N%>%<Pl%>:@R.poss@huan # ;
@R.poss@%<N%>:@R.poss@ Diminutive ;


LEXICON Diminutive
%<Dim1%>:^tzin # ;
%<Dim2%>:^tzintli # ;
%<Pl%>%<Dim%>:^tzitzin # ;